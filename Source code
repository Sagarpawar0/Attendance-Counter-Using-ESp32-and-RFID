#include <SPI.h>
#include <MFRC522.h>
#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <WiFi.h>
#include <HTTPClient.h>

// WiFi details
const char* ssid = "ABCDE";          // your WiFi name
const char* password = "11110000";   // your WiFi password

// Google Apps Script Web App URL
String scriptURL = "https://script.google.com/macros/s/AKfycbxsQD6r6py6T3bW1udNvdXu3aflnp15VogBvZKAIxclgJRMhy9pir8QbVt8PMfqhUpD/exec?";

// RC522 pins
#define SS_PIN 5
#define RST_PIN 4

LiquidCrystal_I2C lcd(0x27, 16, 2);
MFRC522 mfrc522(SS_PIN, RST_PIN);

// Student database
struct Student {
  const char* uid;
  const char* name;
  const char* roll;
};

Student students[] = {
  {"13D348E2", "Sagar Pawar", "EC4233"},
  {"A3F3EEE4", "Sakshi Pawar", "EC4234"},
  {"D0467F5F", "Vaishnavi P.", "EC4231"}
};

int totalStudents = 3;

// Convert UID to string
String getUID(MFRC522::Uid uid) {
  String s = "";
  for (byte i = 0; i < uid.size; i++) {
    if (uid.uidByte[i] < 0x10) s += "0";
    s += String(uid.uidByte[i], HEX);
  }
  s.toUpperCase();
  return s;
}

// Send data to Google Sheets with URL encoding
void sendToGoogle(String name, String roll, String uid) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;

    // Encode spaces
    name.replace(" ", "%20");
    roll.replace(" ", "%20");
    uid.replace(" ", "%20");

    String url = scriptURL + "name=" + name + "&roll=" + roll + "&uid=" + uid;

    Serial.println("Sending URL: " + url);

    http.begin(url);
    int httpCode = http.GET();
    Serial.print("HTTP Response Code: ");
    Serial.println(httpCode);
    http.end();
  }
}

void showWelcome() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("   WELCOME :)   ");
  lcd.setCursor(0, 1);
  lcd.print("Smart Attendance");
  delay(2000);
}

void setup() {
  Serial.begin(115200);
  Wire.begin(21, 22);

  lcd.begin();
  lcd.backlight();

  // WiFi connection
  lcd.clear();
  lcd.print("Connecting WiFi...");
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(300);
    lcd.print(".");
    Serial.print(".");
  }

  lcd.clear();
  lcd.print("WiFi Connected");
  Serial.println("WiFi Connected");
  delay(1000);

  // RFID setup
  SPI.begin(18, 19, 23, SS_PIN);
  mfrc522.PCD_Init();

  showWelcome();
}

void loop() {

  if (!mfrc522.PICC_IsNewCardPresent()) return;
  if (!mfrc522.PICC_ReadCardSerial()) return;

  String cardUID = getUID(mfrc522.uid);
  bool found = false;

  for (int i = 0; i < totalStudents; i++) {

    if (cardUID == students[i].uid) {

      lcd.clear();
      lcd.setCursor(0, 0);
      lcd.print(students[i].name);

      lcd.setCursor(0, 1);
      lcd.print("Roll: ");
      lcd.print(students[i].roll);
      delay(2000);

      lcd.clear();
      lcd.print("Sending Data...");

      sendToGoogle(students[i].name, students[i].roll, students[i].uid);

      lcd.clear();
      lcd.print("Attendance");
      lcd.setCursor(0, 1);
      lcd.print("Marked Online");
      delay(2000);

      found = true;
      break;
    }
  }

  if (!found) {
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Unknown Card");
    lcd.setCursor(0, 1);
    lcd.print("Access Denied");
    delay(2000);
  }

  mfrc522.PICC_HaltA();
  mfrc522.PCD_StopCrypto1();
}
